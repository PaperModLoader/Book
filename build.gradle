plugins {
    id "java"
    id "idea"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group = "xyz.papermodloader"
version = getVersion()
archivesBaseName = "book"
sourceCompatibility = targetCompatibility = "1.8"

if (System.getenv().BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

dependencies {
    compile "org.ow2.asm:asm-debug-all:5.1"
    compile "com.google.code.gson:gson:2.3.1"
    compile "org.apache.commons:commons-lang3:3.4"
}

String getVersion() {
    String version = "0.0.0"
    String prefix = "public static final String VERSION = \"";
    File file = file("src/main/java/xyz/papermodloader/book/Book.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            version = s;
        }
    }
    return version;
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "xyz.papermodloader"
            artifactId "paper"
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                if (System.getenv().PASSWORD) {
                    username System.getenv().USERNAME
                    password System.getenv().PASSWORD
                } else {
                    username ""
                    password ""
                }
            }
            if (System.getenv().PASSWORD) {
                url System.getenv().REPO
            } else {
                url "file:///var/www/maven/"
            }
        }
    }
}
